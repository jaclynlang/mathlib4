/-
Copyright (c) 2024 Christian Merten. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Christian Merten
-/
import Mathlib.AlgebraicGeometry.Morphisms.FiniteType
import Mathlib.RingTheory.RingHom.FinitePresentation

/-!
# Morphisms of finite presentation

A morphism of schemes `f : X âŸ¶ Y` is locally of finite presentation if for each affine `U âŠ† Y` and
`V âŠ† f â»Â¹' U`, The induced map `Î“(Y, U) âŸ¶ Î“(X, V)` is of finite presentation.

A morphism of schemes is of finite presentation if it is both locally of finite presentation and
quasi-compact.

We show that these properties are local, and are stable under compositions.

-/


noncomputable section

open CategoryTheory CategoryTheory.Limits Opposite TopologicalSpace

universe v u

namespace AlgebraicGeometry

variable {X Y : Scheme.{u}} (f : X âŸ¶ Y)

/-- A morphism of schemes `f : X âŸ¶ Y` is locally of finite presentation if for each affine `U âŠ† Y`
and `V âŠ† f â»Â¹' U`, The induced map `Î“(Y, U) âŸ¶ Î“(X, V)` is of finite presentation.
-/
@[mk_iff]
class IsLocallyOfFinitePresentation (f : X âŸ¶ Y) : Prop where
  finitePresentation_of_affine_subset :
    âˆ€ (U : Y.affineOpens) (V : X.affineOpens) (e : V.1 â‰¤ (Opens.map f.1.base).obj U.1),
      (Scheme.Hom.appLe f e).FinitePresentation

namespace IsLocallyOfFinitePresentation

lemma finitePresentation_of_affine_subset' [IsLocallyOfFinitePresentation f]
    (U : Y.affineOpens) (V : X.affineOpens)
    (e : V.1 â‰¤ (Opens.map f.1.base).obj U.1) :
    (Scheme.Hom.appLe f e).FinitePresentation :=
  â€¹IsLocallyOfFinitePresentation fâ€º.finitePresentation_of_affine_subset U V e

lemma isLocallyOfFinitePresentation_eq :
    @IsLocallyOfFinitePresentation = affineLocally @RingHom.FinitePresentation := by
  ext X Y f
  rw [isLocallyOfFinitePresentation_iff, affineLocally_iff_affineOpens_le]
  exact RingHom.finitePresentation_respectsIso

instance (priority := 900) of_isOpenImmersion [IsOpenImmersion f] :
    IsLocallyOfFinitePresentation f :=
  isLocallyOfFinitePresentation_eq.symm â–¸
    RingHom.finitePresentation_is_local.affineLocally_of_isOpenImmersion f

instance (priority := 900) [IsLocallyOfFinitePresentation f] : LocallyOfFiniteType f :=
  âŸ¨fun U V e â†¦
    RingHom.FiniteType.of_finitePresentation (finitePresentation_of_affine_subset' f U V e)âŸ©

instance stableUnderComposition :
    MorphismProperty.IsStableUnderComposition @IsLocallyOfFinitePresentation :=
  isLocallyOfFinitePresentation_eq.symm â–¸
    RingHom.finitePresentation_is_local.affineLocally_isStableUnderComposition

instance comp {Z : Scheme} (g : Y âŸ¶ Z)
    [hf : IsLocallyOfFinitePresentation f] [hg : IsLocallyOfFinitePresentation g] :
    IsLocallyOfFinitePresentation (f â‰« g) :=
  MorphismProperty.comp_mem _ f g hf hg

lemma affine_openCover_iff
    (ğ’° : Scheme.OpenCover.{u} Y) [âˆ€ i, IsAffine (ğ’°.obj i)]
    (ğ’°' : âˆ€ i, Scheme.OpenCover.{u} ((ğ’°.pullbackCover f).obj i)) [âˆ€ i j, IsAffine ((ğ’°' i).obj j)] :
    IsLocallyOfFinitePresentation f â†”
      âˆ€ i j, (Scheme.Î“.map ((ğ’°' i).map j â‰« pullback.snd).op).FinitePresentation :=
  isLocallyOfFinitePresentation_eq.symm â–¸
    RingHom.finitePresentation_is_local.affine_openCover_iff f ğ’° ğ’°'

lemma source_openCover_iff
    (ğ’° : Scheme.OpenCover.{u} X) : IsLocallyOfFinitePresentation f â†”
      âˆ€ i, IsLocallyOfFinitePresentation (ğ’°.map i â‰« f) :=
  isLocallyOfFinitePresentation_eq.symm â–¸
    RingHom.finitePresentation_is_local.source_openCover_iff f ğ’°

lemma openCover_iff (ğ’° : Scheme.OpenCover.{u} Y) :
    IsLocallyOfFinitePresentation f â†”
      âˆ€ i, IsLocallyOfFinitePresentation (pullback.snd : pullback f (ğ’°.map i) âŸ¶ _) :=
  isLocallyOfFinitePresentation_eq.symm â–¸
    RingHom.finitePresentation_is_local.is_local_affineLocally.openCover_iff f ğ’°

lemma respectsIso :
    MorphismProperty.RespectsIso @IsLocallyOfFinitePresentation :=
  isLocallyOfFinitePresentation_eq.symm â–¸
    targetAffineLocally_respectsIso
      (sourceAffineLocally_respectsIso RingHom.finitePresentation_respectsIso)

end IsLocallyOfFinitePresentation

end AlgebraicGeometry
